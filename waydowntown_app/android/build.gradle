allprojects {
    repositories {
        google()
        mavenCentral()
    }

    // Fix for AGP 8.x: auto-set namespace for plugins that don't specify one
    pluginManager.withPlugin("com.android.library") {
        def androidExtension = project.extensions.getByType(com.android.build.gradle.LibraryExtension)
        if (androidExtension.namespace == null) {
            def manifestFile = project.file("src/main/AndroidManifest.xml")
            if (manifestFile.exists()) {
                def manifest = new XmlSlurper().parse(manifestFile)
                def packageName = manifest.@package.toString()
                if (packageName) {
                    androidExtension.namespace = packageName
                }
            }
        }
    }
}

rootProject.buildDir = "../build"
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
